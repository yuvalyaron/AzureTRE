# This file has a .terraform file extension in order to avoid 'terraform init's validation checks that are executed by the 'make bundle-build' command.
# The Dockerfile includes a RUN command to change the extension from .terraform to .tf after the files from the base workspace are copied to this directory.

locals {
  core_resource_group_name = "rg-${var.tre_id}"
  # STorage AirLock IMport InProgress
  import_in_progress_storage_name = lower(replace("stalimip${var.tre_id}", "-", ""))
}

module "terraform_azurerm_environment_configuration" {
  source          = "github.com/microsoft/terraform-azurerm-environment-configuration"
  arm_environment = var.arm_environment
}

data "azurerm_storage_account" "sa_import_inprogress" {
  name                = local.import_in_progress_storage_name
  resource_group_name = local.core_resource_group_name
}

data "azurerm_private_dns_zone" "blobcore" {
  name                = module.terraform_azurerm_environment_configuration.private_links["privatelink.blob.core.windows.net"]
  resource_group_name = local.core_resource_group_name
}

resource "azurerm_private_endpoint" "sa_import_inprogress_pe" {
  name                = "stg-ip-import-blob-${local.workspace_resource_name_suffix}"
  location            = var.location
  resource_group_name = azurerm_resource_group.ws.name
  subnet_id           = module.network.services_subnet_id

  lifecycle { ignore_changes = [tags] }

  private_dns_zone_group {
    name                 = "pdzg-stg-ip-import-blob-${local.workspace_resource_name_suffix}"
    private_dns_zone_ids = [data.azurerm_private_dns_zone.blobcore.id]
  }

  private_service_connection {
    name                           = "psc-stg-ip-import-blob-${local.workspace_resource_name_suffix}"
    private_connection_resource_id = data.azurerm_storage_account.sa_import_inprogress.id
    is_manual_connection           = false
    subresource_names              = ["Blob"]
  }

  tags = local.tre_workspace_tags
}
